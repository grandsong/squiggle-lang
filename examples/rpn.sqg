let {Number, console} = global

let text = "2 3 4 * 3 - +"

# You have to explicitly ignore arguments
# with an underscore in Squiggle.
def tokenize(text)
    text.split(" ").map(fn(x, ...) tokenValue(x))
end

def tokenValue(token)
    match token
    case "+" then ["function", fn(a, b) a + b]
    case "-" then ["function", fn(a, b) a - b]
    case "*" then ["function", fn(a, b) a * b]
    case "/" then ["function", fn(a, b) a / b]
    case num then ["number", Number(num)]
    end
end

def evaluateWithStack(stack, values)
    match [stack, values]
    case [[first, second, ...rest], [["function", f], ...xs]] then
        evaluateWithStack([f(first, second)] ++ rest, xs)
    case [stack, [["number", n], ...xs]] then
        evaluateWithStack([n] ++ stack, xs)
    case [[result, ...], _] then
        result
    end
end

def evaluate(text)
    evaluateWithStack([], tokenize(text))
end

console.log(evaluate(text))
